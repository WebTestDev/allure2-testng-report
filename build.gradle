plugins {
    id 'io.franzbecker.gradle-lombok' version '1.8'
    id 'java'
    id 'ru.vyarus.quality' version '2.2.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
    agent
}

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

group 'io.github.sskorol'
description 'Allure 2 integration with TestNG'

dependencies {
    agent "org.aspectj:aspectjweaver:1.8.10"
    compile("org.assertj:assertj-core:3.6.2",
            "org.jooq:joor:0.9.6",
            "io.qameta.allure:allure-testng:2.0-BETA7",
            "org.slf4j:slf4j-simple:1.7.25")
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

task copyConfig(type: Copy) {
    from 'src/test/resources/config'
    into 'build/allure-results'
}

task copyHistory(type: Copy) {
    from 'src/test/resources/history'
    into 'build/allure-results/history'
}

test.doFirst {
    jvmArgs "-javaagent:${configurations.agent.singleFile}"
}

test {
    useTestNG() {
        listeners << 'io.github.sskorol.listeners.BaseListener'
        suites 'src/test/resources/suites/parent.xml'
    }

    systemProperty 'allure.model.indentOutput', true
    systemProperty 'allure.results.directory', 'build/allure-results'
    systemProperty 'allure.link.issue.pattern', 'https://github.com/allure-framework/allure-docs/issues/{}'
    systemProperty 'allure.link.tms.pattern', 'https://github.com/allure-framework/allure-docs/issues/{}'
}

test.dependsOn copyConfig, copyHistory

lombok {
    version = '1.16.16'
}